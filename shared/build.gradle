plugins {
    id 'java-library'
    id 'groovy'
    id 'java-test-fixtures'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    implementation 'org.reflections:reflections:0.10.2'

    api 'javax.inject:javax.inject:1'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    //logging
    implementation 'org.slf4j:slf4j-api:2.0.11'
    implementation 'ch.qos.logback:logback-classic:1.4.14'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

    //jackson
    implementation 'com.fasterxml.jackson.core:jackson-core:2.16.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.16.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.16.1'

    //fhir
    api 'ca.uhn.hapi.fhir:hapi-fhir-base:6.10.2'
    api 'ca.uhn.hapi.fhir:hapi-fhir-structures-r4:6.10.2'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-caching-caffeine:6.10.2'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-validation-resources-r4:6.10.2'
    api 'org.fhir:ucum:1.0.8'

    // Apache Client
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.3'
    implementation 'org.apache.httpcomponents.client5:httpclient5-fluent:5.2.3'

    // jjwt
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // azure sdk
    implementation 'com.azure:azure-security-keyvault-secrets:4.7.3'
    implementation 'com.azure:azure-identity:1.11.1'

    testImplementation 'org.apache.groovy:groovy:4.0.18'
    testFixturesImplementation 'org.apache.groovy:groovy:4.0.18'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testFixturesImplementation 'com.openpojo:openpojo:0.9.1'
    testFixturesImplementation 'nl.jqno.equalsverifier:equalsverifier:3.15.6'

    // dotenv-java
    implementation 'io.github.cdimascio:dotenv-java:3.0.0'

    // postgres
    implementation 'org.postgresql:postgresql:42.7.1'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.90
            }
        }
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, excludes: rootProject.ext.jacoco_excludes)
        }))
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, excludes: rootProject.ext.jacoco_excludes)
        }))
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
